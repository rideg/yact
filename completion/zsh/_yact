#compdef yact
#autoload
#
zmodload zsh/mapfile

test -z "$YACT_DIR" && YACT_DIR=~/.yact
STORAGE_DIR=$YACT_DIR/storage

__yact_list_commands() {
  local -a list
  list=(
  new:'Creates a new list.'
  delete:'Deletes an existing list'
  modify:'Modifies the description of the current a list'
  switch:'Switches to the given list'
  show:'Shows all extisting lists'
  )
  _describe -t list-commands 'list related commands' list
}

__yact_config_commands() {
  local -a list
  list=(
  set:'Sets the given configuration option'
  get:'Gets the given configuration option'
  )
  _describe -t config-commands 'config related commands' list
}

__yact_task_commands() {
  local -a list
  list=(
  add:'Adds a new task to the current list'
  delete:'Removes the given task from the current list'
  'done':'Marks the given task as completed'
  undone:'Marks the given task as not yet finished'
  show:'Shows the tasks from the current list'
  modify:'Modifies the a given task'
  move:'Moves the given task to a given position'
  swap:'Swaps two tasks on the current list'
  reverse:'Reverses the tasks on the current list'
  )
  _describe -t task-commands 'task related commands' list
}

__yact_admin_commands() {
  local -a list
  list=(
    migrate:'Migrate file storage'
    config:'Configure global variables'
  )
  _describe -t admin-commands 'admin related commands' list
}

__show_list_ids() {
  local -a list
  list=()
	local -i index=1
  for filename in "$STORAGE_DIR"/*.txt; do
    list+=($index:"$(head -n 1 "$filename")")
		((index++))
  done
  if [[ ${#list} -ne 0 ]]; then
    _describe -t list-ids 'list ids' list
  else
    _message -r "Cannot find list, have you created any?"
  fi
}

__yact_list_tasks_for_delete() {
  __yact_list_tasks delete
}

__yact_list_tasks() {
  local file
  local cmd;
  cmd=$1
  # shellcheck source=/dev/null
  test -e $YACT_DIR/.run/.last && . "$YACT_DIR"/.run/.last
  file=$TODO_FILE
  if [[ -n "$file" ]] && [[ -e "$file" ]]; then
    declare -a list description
    local max_id
    local max_width
    max_id=("${(f@)mapfile[$file]}")
    max_id=${#max_id[@]}
    max_width=${#max_id}
    local id=1
    while IFS=';' read -r type task task_status; do
      if [[ ( ($cmd == 'done' &&  "$task_status" -eq 0) \
        || ($cmd == 'undone' && "$task_status" -eq 1 ) \
        || $cmd == 'delete' || $cmd == 'modify' \
        || $cmd == 'move' || $cmd == 'swap' ) && -n "$task" ]]; then
      list+=("$id")
      description+=("$(printf "%-${max_width}d -- %s" "$id" "$task")")
      fi
      ((id++))
  done <<<"$(sed '1,2d'  "$file")"
  if [[ ${#list} -ne 0 && ${#list} -gt 0 ]]; then
    compadd -V "task-ids" -Q -l -d description -a list
  else
    _message -r "There is no appropriate task on this list."
  fi
else
  _message -r "You don't have any list, please create on first"
fi
}

__extended_task_id_list() {
  local max_id
  local current=1
  declare -a list
  list+=($@)
  test -e $YACT_DIR/run/.last && . "$YACT_DIR"/run/.last
  file=$STORAGE_DIR/$TODO_FILE

  max_id=("${(f@)mapfile[$file]}")
  max_id=${#max_id[@]}
  while [[ $current -le $max_id ]]; do
    list+=("$current")
    ((current++))
  done
  compadd -V "extended-ids" -Q -d list -a list
}

__yact_handle_tasks() {
   if [[ "$words[2]" == '-l' ]]; then
      case "$words[3]" in
	 delete|switch|modify) __show_list_ids;;
      esac
   elif [[ "$words[2]" == '-a' ]]; then
      if [[ "$words[3]" == 'config' ]]; then
	  __yact_config_commands
      fi
   else
     __yact_task_commands
   fi
}

__yact_list_config_options() {
   declare -a list
   IFS=$'\n' list=($(yact -a config -c))
  _describe -t config-options 'Configuration options' list

}

__yact_handle_args() {
     case "$words[1]" in
        delete)
          if [[ ${#words[@]} -eq 2 \
            || ( ${#words[@]} -eq 3 \
            && $words[2] == '-f' ) ]]; then
          _arguments \
            '-f[Force delete]' \
            '*: :__yact_list_tasks_for_delete'
        fi
        ;;
      ('done'|undone|modify)
        if [[ ${#words[@]} -eq 2 ]]; then
          __yact_list_tasks "$words[1]"
        fi
        ;;
      move)
        if [[ ${#words[@]} -eq 2 ]]; then
          __yact_list_tasks move
        elif [[ ${#words[@]} -eq 3 ]]; then
          __extended_task_id_list top bottom up down
        fi
        ;;
      swap)
        if [[ ${#words[@]} -eq 2 ]]; then
          __yact_list_tasks swap
        elif [[ ${#words[@]} -eq 3 ]]; then
          __yact_list_tasks swap
        fi
        ;;
      add)
        if [[ ${#words[@]} -eq 2 ]]; then
          _arguments '-p[Insert into a given position]' \
                     '-s[Insert separator]' \
                     '*:: :->arg'
        elif [[ \
          (${#words[@]} -eq 4 && ${words[2]} == '-s' && ${words[3]} == '-p') || \
          (${#words[@]} -eq 3 && ${words[2]} == '-p') ]]; then
          __extended_task_id_list top bottom
        elif [[ ${#words[@]} -eq 4 && ${words[2]} == '-p' ]]; then
          _arguments '-s[Insert separator]' \
            '*:: :->arg'
        elif [[ ${#words[@]} -eq 3 && ${words[2]} == '-s' ]]; then
          _arguments '-p[Insert into a given position]' \
            '*:: :->arg'
        fi
	;;
      'set'|get)
	__yact_list_config_options
        ;;
    esac
}

__yact_main() {
  local curcontext="$curcontext" state line
  typeset -A opt_args

  _arguments -C \
    '-l[Work with lists]: :->lists' \
    '-v[version]: :->end' \
    '-h[help]: :->end' \
    '-a[admin]: :->admin' \
    ': :->tasks' \
    '(-)*:: :->arg' && return

  case "$state" in
    (tasks)
      __yact_handle_tasks
      ;;
    (lists)
      __yact_list_commands
      ;;
    (admin)
      __yact_admin_commands
      ;;
    (arg)
      __yact_handle_args
    ;;
esac
}

__yact_main

